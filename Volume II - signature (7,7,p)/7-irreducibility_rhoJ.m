
/*
    We check that the mod p representation associated with J = J_7 is absolutely irreducible.
*/

load "7-fieldK.m";

print "";
print "The field K is generated by z with minimal polynomial:";
print MinimalPolynomial(z);

print "";
print "The unit group of K is:";
print U;
print "and is generated by {-1, eps1, eps2} where eps1 =", K!phi(U.2)," and eps2 =", K!phi(U.3);
print "";

Pr:=[u[1] : u in Factorization(Gcd(Integers()!Norm(K!phi(U.2)^(84)-1),Integers()!Norm(K!phi(U.3)^(84)-1))) | u[1] notin {2,3,7}];
print "The prime numbers p with p = 5 or p > 7 such that and p divides gcd(Norm(eps_1^84 - 1),Norm(eps_2^84 - 1)) are:";
print Pr;
print "";

for i in [1..#Pr] do
    p:=Pr[i];
    I:=Factorization(p*OK);
    if #I eq 3 then
        print "Dealing with the split prime p =",p;
        orders:=[];
        for j in [1..3] do
            Append(~orders,Order(RayClassGroup(I2*I7*I[j][1],[1,2,3])));
        end for;
        print "For the three prime ideals P |",p," the orders of the Ray Class Groups of modulus Q2*Q7*P*oo1*oo2*oo3 are:",orders;
    end if;
end for;
